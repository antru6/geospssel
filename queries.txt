######################################################################
## Q1
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?s rdf:type gadm:AdministrativeUnit3.
  ?s gadm:has_NAME_1 ?adm1 .
  ?s gadm:has_NAME_2 ?adm2 .
  ?s gadm:has_NAME_3 ?adm3.
  ?s geo:hasGeometry ?g .
  ?g geo:asWKT ?wkt .
    
  FILTER (geof:sfIntersects(?wkt, "POLYGON ((15.11 48.68, 15.11 48.72, 15.19 48.72, 15.19 48.68, 15.11 48.68))"^^geo:wktLiteral)).
}

######################################################################
## Q2
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?s rdf:type gadm:AdministrativeUnit3 .
  ?s gadm:has_NAME_1 ?adm1 .
  ?s gadm:has_NAME_2 ?adm2 .
  ?s gadm:has_NAME_3 ?adm3.
  ?s geo:hasGeometry ?g .
  ?g geo:asWKT ?wkt .
    
  FILTER (geof:sfIntersects(?wkt, "POLYGON ((14.87 48.18, 14.87 48.14, 14.94 48.14, 14.94 48.18, 14.87 48.18))"^^geo:wktLiteral)).
}

######################################################################
## Q3
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?u rdf:type gadm:AdministrativeUnit3 .
  ?u gadm:has_NAME_3 "Deutsch Kaltenbrunn" .
  ?u geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt1 .
    
  ?s rdf:type fs:FoodSecurityObservation .
  ?s fs:hasDN ?dn .
  ?s fs:hasRECDATE "2018-02-10T00:00:00"^^xsd:dateTime .
  ?s geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt2 .
  
  FILTER (geof:distance(?wkt1, ?wkt2, uom:metre) < 5000) .
}

######################################################################
## Q4
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?u rdf:type gadm:AdministrativeUnit3 .
  ?u gadm:has_NAME_3 "Kirchberg am Walde" .
  ?u geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt1 .
    
  ?f invekos:hasID ?id .
  ?f invekos:hasCropTypeName "POTATO" .
  ?f invekos:hasArea ?area .
  ?f geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt3 .
    
  FILTER (geof:sfWithin(?wkt3, ?wkt1)).
}

######################################################################
## Q5
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?f invekos:hasID ?id .
  ?f invekos:hasCropTypeName "POTATO" .
  ?f invekos:hasArea ?area .
  ?f geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt3 .
    
  ?s rdf:type fs:FoodSecurityObservation .
  ?s fs:hasDN ?dn .
  ?s fs:hasRECDATE "2018-02-10T00:00:00"^^xsd:dateTime .
  ?s geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt2 .
  FILTER (0 < ?dn && ?dn < 50) .
  
  FILTER (geof:sfIntersects(?wkt3, ?wkt2)).
  FILTER (geof:sfIntersects(?wkt3, "POLYGON ((15.11 48.68, 15.11 48.72, 15.19 48.72, 15.19 48.68, 15.11 48.68))"^^geo:wktLiteral)).
  FILTER (geof:sfIntersects(?wkt2, "POLYGON ((15.11 48.68, 15.11 48.72, 15.19 48.72, 15.19 48.68, 15.11 48.68))"^^geo:wktLiteral)).
}

######################################################################
## Q6
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?u rdf:type gadm:AdministrativeUnit3 .
  ?u gadm:has_NAME_3 "Kirchberg am Walde" .
  ?u geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt1 .
    
  ?f invekos:hasID ?id .
  ?f invekos:hasCropTypeName "POTATO" .
  ?f invekos:hasArea ?area .
  ?f geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt2 .
    
  ?s rdf:type fs:FoodSecurityObservation .
  ?s fs:hasDN ?dn .
  ?s fs:hasRECDATE "2018-02-10T00:00:00"^^xsd:dateTime .
  ?s geo:hasGeometry ?z .
  ?z geo:asWKT ?wkt3 .
  FILTER (0 < ?dn && ?dn < 50) .
  
  FILTER (geof:sfWithin(?wkt2, ?wkt1)).
  FILTER (geof:sfWithin(?wkt2, ?wkt3)).
  FILTER (geof:sfIntersects(?wkt1, ?wkt3)).
}

######################################################################
## Q7
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?f invekos:hasID ?id .
  ?f invekos:hasCropTypeName "POTATO" .
  ?f invekos:hasArea ?area .
  ?f geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt2 .
  
  ?s rdf:type fs:FoodSecurityObservation .
  ?s fs:hasDN ?dn .
  ?s fs:hasRECDATE "2018-02-10T00:00:00"^^xsd:dateTime .
  ?s geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt3 .
  FILTER (0 < ?dn && ?dn < 50) .
  
  FILTER (geof:distance(?wkt2, ?wkt3, uom:metre) < 5000).
  FILTER (geof:sfIntersects(?wkt2, "POLYGON ((15.11 48.68, 15.11 48.72, 15.19 48.72, 15.19 48.68, 15.11 48.68))"^^geo:wktLiteral)).
  FILTER (geof:sfIntersects(?wkt3, "POLYGON ((15.11 48.68, 15.11 48.72, 15.19 48.72, 15.19 48.68, 15.11 48.68))"^^geo:wktLiteral)).
}

######################################################################
## Q8
######################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX gadm: <http://gadm.org/ontology#>
PREFIX fs: <http://example.org/fs/ontology/>
PREFIX invekos: <http://example.org/invekos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT * WHERE {
  ?u rdf:type gadm:AdministrativeUnit3 .
  ?u gadm:has_NAME_3 "Deutsch Kaltenbrunn" .
  ?u geo:hasGeometry ?x .
  ?x geo:asWKT ?wkt1 .
  
  ?f invekos:hasID ?id .
  ?f invekos:hasCropTypeName "POTATO" .
  ?f invekos:hasArea ?area .
  ?f geo:hasGeometry ?y .
  ?y geo:asWKT ?wkt2 .
  
  ?s rdf:type fs:FoodSecurityObservation .
  ?s fs:hasDN ?dn .
  ?s fs:hasRECDATE "2018-02-10T00:00:00"^^xsd:dateTime .
  ?s geo:hasGeometry ?z .
  ?z geo:asWKT ?wkt3 .
  FILTER (0 < ?dn && ?dn < 60) .
  
  FILTER (geof:sfWithin(?wkt2, ?wkt1)).
  FILTER (geof:distance(?wkt2, ?wkt3, uom:metre) < 5000).
  FILTER (geof:sfIntersects(?wkt1, ?wkt3)).
}
